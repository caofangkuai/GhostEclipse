plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

def keystoreFilepath = ''
def keystorePSW = ''
def keystoreAlias = ''
def keystoreAliasPSW = ''
// default keystore file, PLZ config file path in local.properties
def keyfile = file('s.keystore.temp')

Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyfile = file(keystoreFilepath)
}

android {
    namespace 'cfks.bugjump.ghosteclipse'
    compileSdk 34
    
    defaultConfig {
        applicationId "cfks.bugjump.ghosteclipse"
        minSdk 26
        targetSdk 28
        versionCode 1
        versionName "1.0"
        
        vectorDrawables { 
            useSupportLibrary true
        }
    }
	
	signingConfigs {
       release {
           keyAlias keystoreAlias
           keyPassword keystoreAliasPSW
           storeFile keyfile
           storePassword keystorePSW
           
           enableV1Signing true
           enableV2Signing true
           enableV3Signing true
           enableV4Signing false
       }
       
       debug {
           keyAlias keystoreAlias
           keyPassword keystoreAliasPSW
           storeFile keyfile
           storePassword keystorePSW
           
           enableV1Signing true
           enableV2Signing true
           enableV3Signing true
           enableV4Signing false
       }
   }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //signingConfig signingConfigs.release
            //签名文件存在，则签名
            if (keyfile.exists()) {
               println("WITH -> buildTypes -> release: using jks key")
               signingConfig signingConfigs.release
            } else {
               println("WITH -> buildTypes -> release: using default key")
            }
        }
		
        debug{
            if (keyfile.exists()) {
               println("WITH -> buildTypes -> debug: using jks key")
               signingConfig signingConfigs.debug
            } else {
               println("WITH -> buildTypes -> debug: using default key")
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    
    buildFeatures {
        viewBinding true
    }
    
    packagingOptions {
        exclude 'META-INF/**'
        exclude "kotlin/**"
        exclude "DebugProbesKt.bin"
        exclude "kotlin-tooling-metadata.json"
    }
    
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "17"
  }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.5.1"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.lifecycle:lifecycle-livedata:2.5.1"
    implementation "androidx.compose.ui:ui-graphics:1.7.8"
    implementation "androidx.compose.material3:material3:1.1.0"
    implementation "com.google.accompanist:accompanist-drawablepainter:0.30.1"
    implementation "androidx.compose.foundation:foundation-android:1.7.7"
    implementation "com.google.android.material:material:1.9.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
	implementation "com.github.yangchong211.YCAndroidTool:MonitorCrashLib:1.2.8"
    implementation 'com.geyifeng.immersionbar:immersionbar:3.2.2'
    implementation "com.github.kongzue.DialogX:DialogX:0.0.50.beta38"
    implementation "com.github.kongzue.DialogX:DialogXMaterialYou:0.0.50.beta38"
    implementation "com.github.knightwood:material3-preference:1.4"
    // java版
    implementation('org.geysermc.mcprotocollib:protocol:1.21.7-SNAPSHOT'){
        exclude group: 'org.cloudburstmc.math', module: 'api'
        exclude group: 'com.nukkitx.fastutil'
    }
    implementation "org.cloudburstmc.protocol:bedrock-connection:3.0.0.Beta7-SNAPSHOT"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation('coelho.msftauth:minecraft-msftauth:2.1.6') {
        exclude group: 'com.squareup.okhttp3'
    }
    implementation "com.squareup.okhttp3:okhttp:4.11.0"
}
